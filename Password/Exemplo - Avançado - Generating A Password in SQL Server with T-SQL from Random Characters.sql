-- Create the View to store a random number
Create View dbo.vRandomNumber
as
  Select rand() as RandomNumber
Go

-- Create the function
Create function dbo.randbetween(@bottom integer, @top integer)
Returns integer
as
begin
  Return (Select cast(round((@top-@bottom)* RandomNumber +@bottom,0) as integer) from dbo.vRandomNumber)
end
Go

-- test the function --
Select dbo.randbetween(25,75)
Go

-- Create a Table to store values generated by the function --
Create Table dbo.tblRandBetweenTest(
   pkRandBetweenTest int identity (1,1) primary key,
   GeneratedRandomValue integer NOT NULL) 
ON [PRIMARY]
Go

SET NOCOUNT ON
Go

-- test the function 10,000 times --
Declare @i as integer
set @i=1
while @i<=10000
begin
   insert into dbo.tblRandBetweenTest values (dbo.randbetween(25,75))
   set @i=@i+1
end

-- Select the min, max and average --
Select 
min(GeneratedRandomValue) as min_x,
max(GeneratedRandomValue) as max_x,
avg(GeneratedRandomValue) as avg_x
from dbo.tblRandBetweenTest
Go

-- get the population of the values
Select GeneratedRandomValue, count(GeneratedRandomValue) as qty
from dbo.tblRandBetweenTest
group by GeneratedRandomValue
order by GeneratedRandomValue


-- Create Function GeneratePassword --
Create FUNCTION dbo.GeneratePassword ()
ReturnS varchar(10)
AS
BEGIN
  Declare @randInt int;
  Declare @NewCharacter varchar(1); 
  Declare @NewPassword varchar(10); 
  SET @NewPassword='';

  --6 random characters
  WHILE (LEN(@NewPassword) <6)
  BEGIN
    Select @randInt=dbo.randbetween(48,122)
	--      0-9           < = > ? @ A-Z [ \ ]                   a-z      
    IF @randInt<=57 OR (@randInt>=60 AND @randInt<=93) OR (@randInt>=97 AND @randInt<=122)
    Begin
      Select @NewCharacter=CHAR(@randInt)
      Select @NewPassword=CONCAT(@NewPassword, @NewCharacter)
    END
  END

  --Ensure a lowercase
  Select @NewCharacter=CHAR(dbo.randbetween(97,122))
  Select @NewPassword=CONCAT(@NewPassword, @NewCharacter)
  
  --Ensure an upper case
  Select @NewCharacter=CHAR(dbo.randbetween(65,90))
  Select @NewPassword=CONCAT(@NewPassword, @NewCharacter)
  
  --Ensure a number
  Select @NewCharacter=CHAR(dbo.randbetween(48,57))
  Select @NewPassword=CONCAT(@NewPassword, @NewCharacter)
  
  --Ensure a symbol
  WHILE (LEN(@NewPassword) <10)
  BEGIN
    Select @randInt=dbo.randbetween(33,64)
	--           !               # $ % &                            < = > ? @
    IF @randInt=33 OR (@randInt>=35 AND @randInt<=38) OR (@randInt>=60 AND @randInt<=64) 
    Begin
     Select @NewCharacter=CHAR(@randInt)
     Select @NewPassword=CONCAT(@NewPassword, @NewCharacter)
    END
  END

  Return(@NewPassword);
END;
Go

Select dbo.GeneratePassword() AS 'NewPassword';